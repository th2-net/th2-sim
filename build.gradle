/*******************************************************************************
 * Copyright 2020-2020 Exactpro (Exactpro Systems Limited)
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 ******************************************************************************/

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72"
    }
}

plugins {
    id 'com.jfrog.artifactory' version '4.15.1'
    id "com.jfrog.bintray" version '1.8.5'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        sharedDir           = file("${project.rootDir}/shared")
    }

    group = 'com.exactpro.th2'
    version = release_version

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        maven {
            name 'MavenLocal'
            url sharedDir
        }
        mavenCentral()
        maven {
            name 'Bintray'
            url 'https://dl.bintray.com/exactpro/th2'
        }
        mavenLocal()

        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    dependencies {
        api platform('com.exactpro.th2:bom:2.6.+')
        implementation "com.exactpro.th2:grpc-common"
    }

    jar {
        manifest {
            attributes(
                    'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                    'Specification-Title':      '',
                    'Specification-Vendor':     'Exactpro Systems LLC',
                    'Implementation-Title':     project.archivesBaseName,
                    'Implementation-Vendor':    'Exactpro Systems LLC',
                    'Implementation-Vendor-Id': 'com.exactpro',
                    'Implementation-Version':   project.version
            )
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
            bintrayPublication(MavenPublication) {
                from components.java
                groupId group
                artifactId rootProject.name
                version project.version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', rootProject.name)
                    root.appendNode('url', project.vcs_url)
                }
            }
        }

        repositories {
            maven {
                name = 'localRepo'
                url = sharedDir
            }
        }
    }

    bintray {
        user = "${bintray_user}"
        key = "${bintray_key}"
        publications = ['bintrayPublication']
        pkg {
            repo = 'th2'
            name = rootProject.name
            userOrg = 'exactpro'
            licenses = ['Apache-2.0']
            vcsUrl = project.vcs_url
            version {
                name = project.version
                desc = project.version
                released  = new Date()
            }
        }
        publish = true
    }
}

apply plugin: "org.jetbrains.kotlin.jvm"

dependencies {
    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"

    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"

    implementation "org.jetbrains:annotations"

    implementation "org.apache.commons:commons-lang3"

    implementation "com.google.guava:guava"

    implementation "com.rabbitmq:amqp-client"

    implementation "com.google.protobuf:protobuf-java-util"
    implementation "io.grpc:grpc-netty"
    implementation "io.grpc:grpc-protobuf"
    implementation "io.grpc:grpc-stub"

    implementation "com.exactpro.th2:common"
    implementation "com.exactpro.th2:grpc-common"
    implementation "com.exactpro.th2:grpc-sim"

    testImplementation 'junit:junit:4.12'
}

jar {
    from sourceSets.test.output+sourceSets.test.allSource
}

sourceSets {
    main.kotlin.srcDirs += "src/main/kotlin"
    main.java.srcDirs += "src/main/java"
}
